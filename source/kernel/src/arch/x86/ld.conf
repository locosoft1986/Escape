#include <mem/layout.h>
#if defined(__i586__)
OUTPUT_FORMAT("elf32-i386")
#else
OUTPUT_FORMAT("elf64-x86-64")
#endif

ENTRY(bspEntry)

PHDRS
{
	init	PT_LOAD;
	text	PT_LOAD;
	data	PT_LOAD;
	bss		PT_LOAD;
}

SECTIONS
{
	. = 0x100000;

	.init :
	{
		*(.init)
		*(.initdata)
	} : init = 0x90909090

	. += KERNEL_BEGIN;

	.text : AT(ADDR(.text) - KERNEL_BEGIN)
	{
		_btext = .;
		*(.text*)
	} : text = 0x90909090

	.rodata : AT(ADDR(.rodata) - KERNEL_BEGIN)
	{
		*(.rodata*)
	} : text

    .init_array : AT (ADDR (.init_array) - KERNEL_BEGIN)
    {
        PROVIDE (CTORS_BEGIN = .);
        KEEP (*(.init_array .ctors))
        PROVIDE (CTORS_END = .);
    } : data

	.data ALIGN (4096) : AT(ADDR(.data) - KERNEL_BEGIN)
	{
		*(.data)
		*(.data.*)
	} : data

	.bss ALIGN (4096) : AT(ADDR(.bss) - KERNEL_BEGIN)
	{
		*(COMMON*)
		*(.bss*)
		_ebss = .;
	} : bss
}
